/* Cancellable syscall wrapper.  Linux/sparc32 version.
   Copyright (C) 2017 Free Software Foundation, Inc.
   This file is part of the GNU C Library.

   The GNU C Library is free software; you can redistribute it and/or
   modify it under the terms of the GNU Lesser General Public
   License as published by the Free Software Foundation; either
   version 2.1 of the License, or (at your option) any later version.

   The GNU C Library is distributed in the hope that it will be useful,
   but WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
   Lesser General Public License for more details.

   You should have received a copy of the GNU Lesser General Public
   License along with the GNU C Library; if not, see
   <http://www.gnu.org/licenses/>.  */

#include <sysdep.h>

/* long int __syscall_cancel_arch (int *cancelhandling,
				   long int nr,
				   long int arg1,
				   long int arg2,
				   long int arg3,
				   long int arg4,
				   long int arg5,
				   long int arg6)  */

ENTRY (__syscall_cancel_arch)
	save	%sp, -104, %sp

	cfi_window_save
	cfi_register (15, 31)
	cfi_def_cfa_register (30)

	.globl __syscall_cancel_arch_start
__syscall_cancel_arch_start:

	/* if (*cancelhandling & THREAD_CANCELED)
	     __syscall_do_cancel()  */
	ld	[%i0], %g2
	andcc	%g2, 4, %g0
	bne,pn	%icc, 2f

	/* Issue a 6 argument syscall.  */
	mov	%i1, %g1
	mov	%i2, %o0
	mov	%i3, %o1
	mov	%i4, %o2
	mov	%i5, %o3
	ld	[%fp+92], %o4
	ld	[%fp+96], %o5
	ta	0x10

	.globl __syscall_cancel_arch_end
__syscall_cancel_arch_end:
	bcc	1f
	mov	0,%g1
	sub	%g0, %o0, %o0
	mov	1, %g1

1:
	mov	%o0, %i0
	return	%i7+8
	 nop

2:
	call	__syscall_do_cancel, 0
	 nop
	nop

END (__syscall_cancel_arch)
libc_hidden_def (__syscall_cancel_arch)
